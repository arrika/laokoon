//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

String mavenGroupId = 'lt.skankjo.laokoon'
String mavenVersion = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.
    testCompile 'junit:junit:4.11', 'org.hamcrest:hamcrest-all:1.3'
    compile 'ch.qos.logback:logback-classic:1.0.13',
            'com.google.guava:guava:15.0'
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = mavenGroupId
        artifactId = mavenArtifactId
        version = mavenVersion
    }
}

task createSourceFolders(description: 'Creates the source folders if they do not exist.') << {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            logger.info "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}

